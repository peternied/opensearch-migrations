plugins {
    id 'base'
}

def pythonSources = fileTree(dir: projectDir, include: '**/*.py')
def dependencyFiles = files(
    "Pipfile",
    "Pipfile.lock"
).filter { it.exists() }
def venvDir = layout.buildDirectory.dir(".venv")
def pipenvDir = layout.buildDirectory.dir(".pipenv")
def openapiOutFile = layout.buildDirectory.file("openapi.json")

tasks.register('preparePipenv', Exec) {
    mustRunAfter tasks.named('spotlessCheck')
    mustRunAfter tasks.named('spotlessApply')

    inputs.files(dependencyFiles)
    outputs.dirs(venvDir, pipenvDir)

    workingDir projectDir
    // Run everything in one shell so the venv stays active
    commandLine 'bash', '-c', """
        set -e
        python -m venv '${venvDir.get()}'
        source '${venvDir.get()}/bin/activate'
        python -m pip install --upgrade pip pipenv
        export WORKON_HOME="${pipenvDir.get()}"
        export PIPENV_IGNORE_VIRTUALENVS=1
        pipenv install --deploy --dev
    """
    logger.info("Ran pipenv install")
}

tasks.register('generateOpenApiSpec', Exec) {
    dependsOn tasks.named('preparePipenv')
    inputs.files(pythonSources, dependencyFiles)
    outputs.file(openapiOutFile)

    workingDir projectDir
    commandLine 'bash', '-c', """
        set -eo pipefail
        # Activate the venv created by preparePipenv
        source '${venvDir.get()}/bin/activate'
        export WORKON_HOME='${pipenvDir.get()}'
        export PIPENV_IGNORE_VIRTUALENVS=1

        # Run the generator and write directly to the declared output file
        pipenv run python -m console_link.scripts.generate_openapi \
          > '${openapiOutFile.get().asFile.absolutePath}'
    """
}

tasks.named("build") {
    dependsOn tasks.named('generateOpenApiSpec')
}